# If the task is water-jug and there is a jug that is not empty,
# then propose emptying that jug.

sp {water-jug*propose*empty
   (state <s> ^name water-jug
              ^jug <j>)
   (<j> ^contents > 0)
-->
   (<s> ^operator <o> +)
   (<o> ^name empty
        ^empty-jug <j>)}

# RL Value function for the set of actions involving emptying jugs
gp {rl*water-jug*empty
   (state <s> ^name water-jug 
              ^operator <op> +
              ^jug <j1> <j2>)
   (<op> ^name empty 
         ^empty-jug.volume [3 5])
   (<j1> ^volume 3 
         ^contents [0 1 2 3])
   (<j2> ^volume 5 
         ^contents [0 1 2 3 4 5])
-->
   (<s> ^operator <op> = 0)
}

# If the task is water-jug and the empty operator is selected for a given jug, 
# then set that jug's contents to be 0 and its empty to be its volume.
sp {water-jug*apply*empty
   (state <s> ^name water-jug
              ^operator <o>
              ^jug <j>)
   (<o> ^name empty
        ^empty-jug <j>)
   (<j> ^volume <volume>
        ^contents <contents>)
-->
   (<j> ^contents 0
        ^contents <contents> - )}

